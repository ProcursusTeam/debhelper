# A debhelper build system class for Qt projects
# (based on the makefile class).
#
# Copyright: Â© 2010 Kelvin Modderman
# License: GPL-2+

package Debian::Debhelper::Buildsystem::qmake;

use strict;
use warnings;
use File::Spec qw();
use Debian::Debhelper::Dh_Lib qw(dpkg_architecture_value error is_cross_compiling);
use parent qw(Debian::Debhelper::Buildsystem::makefile);

my %OS_MKSPEC_MAPPING = (
	'linux'    => 'linux-g++',
	'kfreebsd' => 'gnukfreebsd-g++',
	'hurd'     => 'hurd-g++',
);

sub DESCRIPTION {
	"qmake (*.pro)";
}

sub check_auto_buildable {
	my $this=shift;
	my @projects=glob($this->get_sourcepath('*.pro'));
	my $ret=0;

	if (@projects > 0) {
		$ret=1;
		# Existence of a Makefile generated by qmake indicates qmake
		# class has already been used by a prior build step, so should
		# be used instead of the parent makefile class.
		my $mf=$this->get_buildpath("Makefile");
		if (-e $mf) {
			$ret = $this->SUPER::check_auto_buildable(@_);
			open(my $fh, '<', $mf)
				or error("unable to open Makefile: $mf");
			while(<$fh>) {
				if (m/^# Generated by qmake/i) {
					$ret++;
					last;
				}
			}
			close($fh);
		}
	}

	return $ret;
}

sub configure {
	my $this=shift;
	my @options;
	my @flags;

	push @options, '-makefile';
	if (is_cross_compiling()) {
		my $host_os = dpkg_architecture_value("DEB_HOST_ARCH_OS");

		if (defined(my $spec = $OS_MKSPEC_MAPPING{$host_os})) {
			push(@options, "-spec", $spec);
		} else {
			error("Cannot cross-compile: Missing entry for HOST OS ${host_os} for qmake's -spec option");
		}
	}

	if ($ENV{CFLAGS}) {
		push @flags, "QMAKE_CFLAGS_RELEASE=$ENV{CFLAGS} $ENV{CPPFLAGS}";
		push @flags, "QMAKE_CFLAGS_DEBUG=$ENV{CFLAGS} $ENV{CPPFLAGS}";
	}
	if ($ENV{CXXFLAGS}) {
		push @flags, "QMAKE_CXXFLAGS_RELEASE=$ENV{CXXFLAGS} $ENV{CPPFLAGS}";
		push @flags, "QMAKE_CXXFLAGS_DEBUG=$ENV{CXXFLAGS} $ENV{CPPFLAGS}";
	}
	if ($ENV{LDFLAGS}) {
		push @flags, "QMAKE_LFLAGS_RELEASE=$ENV{LDFLAGS}";
		push @flags, "QMAKE_LFLAGS_DEBUG=$ENV{LDFLAGS}";
	}
	push @flags, "QMAKE_STRIP=:";
	push @flags, "PREFIX=/usr";

	# prepare the rest of the arguments
	my @args_rest;
	my $param_seen = 0;
	while (my $arg = shift) {
		if ($arg =~ /^-(?:o|config|spec|platform|xspec|xplatform|t|template|tp|template_prefix|unset|query)$/) {
			push @args_rest, $arg;
			push @args_rest, shift;
		} elsif ($arg eq "-set") {
			push @args_rest, $arg;
			push @args_rest, shift;
			push @args_rest, shift;
		} elsif ($arg eq "--") {
			unshift @_, "--";
			last;
		} elsif ($arg !~ /^-|=/ && -e File::Spec->rel2abs($arg, $this->get_sourcedir)) {
			push @args_rest, File::Spec->rel2abs($arg, $this->get_sourcedir);
			$param_seen = 1;
		} else {
			push @args_rest, $arg;
		}
	}
	push @args_rest, $this->get_source_rel2builddir unless $param_seen;

	$this->mkdir_builddir();
	$this->doit_in_builddir($this->_qmake(), @options, @flags, @args_rest, @_);
}

sub install {
	my $this=shift;
	my $destdir=shift;

	# qmake generated Makefiles use INSTALL_ROOT in install target
	# where one would expect DESTDIR to be used.
	$this->SUPER::install($destdir, "INSTALL_ROOT=$destdir", @_);
}

sub _qmake {
	if (is_cross_compiling()) {
		return dpkg_architecture_value("DEB_HOST_GNU_TYPE") . "-qmake";
	}
	return 'qmake';
}

1
